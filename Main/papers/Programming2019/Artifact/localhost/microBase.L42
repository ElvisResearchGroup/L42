{
Bool:{interface
  class method This ()
  method Void #checkTrue() exception Void
  method This & (This that)
  method This | (This that)
  method This ! ()
  }
True:{_private()  implements Bool
  method () (this._private())
  method #checkTrue() (void)
  method &(that) (that)
  method |(that) (this)
  method !() (False())
  }
False:{_private()  implements Bool
  method () (this._private())
  method #checkTrue() (exception void)
  method &(that) (this)
  method |(that) (that)
  method !() (True())
  }

ExitCode:{
  class method
  Library normal(){return {//@exitStatus
     //0
    }}
  class method
  Library failure(){return {//@exitStatus
     //42000
    }}
  }
Alu:{//@plugin
  //L42.is/connected/withAlu
  }
N:{(Library that)
  class method N #numberParser(Library that) (
    N(using Alu
      check stringToInt32(that)
      error void))
  method N +(N that) (
    N(using Alu
      check sumInt32(n1:this.that() n2:that.that())
      error void))
  method N -(N that) (
    N(using Alu
      check subInt32(n1:this.that() n2:that.that())
      error void))
  method N *(N that) (
    N(using Alu
      check mulInt32(n1:this.that() n2:that.that())
      error void))
  method N /(N that) (
    N(using Alu
      check divInt32(n1:this.that() n2:that.that())
      error void))

  method Bool ==(N that) {
    using Alu
      check ifInt32EqualDo(n1:this.that() n2:that.that())
      return True()
    return False()
    }
  method Bool <(N that) {
    using Alu
      check ifInt32GrtDo(n1:this.that() n2:that.that())
      return True()
    return False()
    }
  method Bool <=(N that) {
    using Alu
      check ifInt32GEqDo(n1:this.that() n2:that.that())
      return True()
    return False()
    }
  method Bool !=(N that) {
    using Alu
      check ifInt32EqualDo(n1:this.that() n2:that.that())
      return False()
    return True()
    }
  method Bool >(N that) {
    using Alu
      check ifInt32GEqDo(n1:this.that() n2:that.that())
      return False()
    return True()
    }
  method Bool >=(N that) {
    using Alu
      check ifInt32GrtDo(n1:this.that() n2:that.that())
      return False()
    return True()
    }
  }

S:{#stringParser(Library that)
  method S ++ (S that) (
    S.#stringParser(using Alu
      check stringConcat(s1:this.that(),s2:that.that())
      error void) )
  method Bool != (S that) !(this==that)
  method Bool == (S that) {
    using Alu
      check ifStringEqualDo(s1:this.that(),s2:that.that())
      return True()
    return False()}
  method
  S (N start, N end)
    S.#stringParser(using Alu
      check stringSubstring(this.that(),start:start.that(),end:end.that())
      error void )

  method
  S (N that) this(start:that, end:that+1N)  method N size () (
    N(using Alu
      check stringSize(this.that())
      error void) )
  }

IntrospectionPlugin:{//@plugin
  //L42.is/connected/withItself
  ()
  class method Library sumLib(Library l1,Library l2) using This check sumLib(l1:l1,l2:l2) error {sumLib()}
  class method Library adaptLib(Library l1 Library l2) using This check adaptLib(l1:l1,l2:l2) error {adaptLib()}
  class method Library nameToAdapter(Library that) using This check nameToAdapter(that) error {nameToAdapter()}
  class method Library typeNameToAdapter(class Any that) using This check typeNameToAdapter(that) error {typeNameToAdapter()}
  }
Collections:{()
  class method Library listRow() { _new(Cell head)
   T:{}
    Cell:{interface
      method mut Iterator #vals(Bool that)
      method Cell #next() exception Void
      method Void checkEmpty() exception Void
      method T #inner()
      method Void #checkEnd()
      }
    CellEnd:{() implements Cell
      method #vals(that) (Iterator(this,terminate:that))
      method #next() (exception void)
      method checkEmpty() (exception void)
      method #inner() (error {innerOnCellEnd()})
      method #checkEnd() (void)
      }
    CellNext:{(T elem, Cell _next) implements Cell
      method #vals(that) (Iterator(CellNext(elem:this.elem(), _next:this), terminate:that))
      method #next() ( this._next() )
      method checkEmpty() (void)
      method #inner() (this.elem())
      method #checkEnd() (
        this._next().checkEmpty()
        catch exception (on Void void)
        error {iterationNotComplete()})
      }
    class method
    This () (This._new(head:CellEnd()))
    method
    This #add(T that) (
      This._new(head:CellNext(
        elem:that,
        _next:this.head()))
      )
    class method
    This #begin() This()
    method
    This #end() (
      var res=This()
      with elem in this.vals() (
        res:=res.#add(elem)
        )
      res
      )
    method Bool isEmpty() { with x=this.head() (on CellEnd  return True() default return False() )}
    method T top() ( this.head().#inner() )
    method
    mut Iterator vals() ( this.head().#vals(True()))
    method
    mut Iterator valsCut() ( this.head().#vals(False()))
    method This pop() {
      return This._new(head:this.head().#next())
      catch exception (on Void error {popOnEmpyList()})}
    Iterator:{mut (var Cell that,Bool terminate)
      mut method Void #next() exception Void ( x=this.that().#next(),x.checkEmpty() this.that(x) )
      read method Void #checkEnd() (if this.terminate() (this.that().#checkEnd()))
      read method T #inner() (this.that().#inner())
      read method Void #close() (void)
      }
    }
class method Library list(class Any that){
  map=IntrospectionPlugin.typeNameToAdapter(that)
  tRen=IntrospectionPlugin.nameToAdapter(S"T".that())
  //If I remove this. from under, I get a stupid error msg
  // vars MUST have positions too :(
  return this.list(adapter: IntrospectionPlugin.adaptLib(l1:map,l2:tRen))
  }
class method Library list(Library adapter){
  return IntrospectionPlugin.adaptLib(l1:this.listRow(),l2:adapter)
  }
}

Name:{(Library adapter)
  class method Name #stringParser(Library that) (Name(adapter:
    using IntrospectionPlugin check nameToAdapter(that) error {nameToAdapter()}
    ))
  class method Name (class Any that) (Name(adapter:
    using IntrospectionPlugin check typeNameToAdapter(that) error {typeNameToAdapter()}
    ))
  }
}
