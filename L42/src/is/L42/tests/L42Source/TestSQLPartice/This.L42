reuse [AdamTowel]

LoadJ=Load:{reuse[JavaServer]}
LoadGui=Load:{reuse[GuiBuilder]}

GuiJ=LoadJ(slaveName=S"miniGuiSlave{}")
DbJ=LoadJ(slaveName=S"dbServer{}")
Query=Load:{reuse[Query]}
DB=Query.sql(connectionString=S"jdbc:derby:PersonsGui;create=true",javaServer=DbJ)
IQL=Query.iql(javaServer=GuiJ)
Gui=LoadGui(javaServer=GuiJ)

JavaCode={...}

Person=Data:{S name, I age, I height, I weight}
Persons=Collection.list(Person)

Queries=DB.QueryBox:{
  @Public Insert=DB.query[Void;S;I;I;I]"""
    |INSERT INTO Person (name,age,height,weight)
    |Values (@name,@age,@height,@weight)
    """
  @Public All=DB.query[Persons]"SELECT * FROM Person"
  }
AddPersons=IQL.query[Persons]"""
  | 'Add data for more persons' Pages('Add data for more persons')
  | name 'name' String
  | age  'age'  Integer
  | height 'height' Decimal
  | weight 'weight' Decimal
  """
  
Model=Data:GuiJ.Handler:{
  mut GuiJ j
  mut Queries q
  @GuiJ.Handler mut method Void printAll(S msg)=(
    Debug(S"printAll(%msg) called")
    all=this.#q().all()()
    Debug(S"printAll(%msg) called; will now be %all.toS()")
    this.#j().submitEvent(key='Example.Display, id=S"label1", msg=all.toS())
    this.#j().submitEvent(key='Example.Display, id=S"tableClear", msg=S"")
    for p in all (
      this.#j().submitEvent(key='Example.Display, id=S"tableAdd", msg=S"%p.name(),%p.age(),%p.height(),%p.weight(),")
      )
    whoops DB.Fail, GuiJ.Fail
    )
  @GuiJ.Handler mut method Void addPerson(S msg)=(
    Debug(S"addPerson(%msg) called")
    ps=AddPersons(IQL(this.#j()))()
    Debug(ps)
    for (name,age,height,weight) in ps (
      this.#q().insert()(name=name, age=age height=height weight=weight)
      )
    whoops DB.Fail, IQL.Fail
    )
  }

SetUp={...}
Table=DB.#$of().tables()
Main=(
  q=Queries(DB.#$of())
  q.insert()(name='Bob, age=15\ height=165\ weight=67\)
  j=GuiJ.#$of()
  model=Model(j=j,q=q)
  gui=Gui(j=model.#j(),package=S"miniGui",imports=S"""%
  | %Gui.defaultImports()
  | import javax.swing.table.DefaultTableModel;
  """,
  name='Example,x=800\,y=600\)
  b1=gui.button(id=S"addPerson",msg='PressedAdd,text=S"add")
  gui"%JavaCode(gui)"
  for e in model.#j()(\['Example]) (e>>model )
  )
TearDown={...}