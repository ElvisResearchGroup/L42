Size side
mut Random r
Size waterLevel
Size treeLevel
Size rockLevel
Size maxZ
HeightMap=Class:Data:MapList.expansion(Size.List,base='zero()):{...}
RiverMap=Class:Data:MapList.expansion(Size.List,base='zero()):{
  read method Size combine(Size x,Size y,Size seed,Size oldCell)=this.baseCombine(x=x,y=y,seed=seed,oldCell=oldCell)
  }
BoolMap=Class:Data:MapList.expansion(Bool.List,base='false()):{
  read method Bool combine(Size x,Size y,Bool seed,Bool oldCell)=this.baseCombine(x=x,y=y,seed=seed,oldCell=oldCell)
  }
mut HeightMap map
mut RiverMap riverMap
mut BoolMap grassMap
mut BoolMap treesMap
mut BoolMap rocksMap


class method mut This(
    Size side,mut Random r,
    Size waterLevel,Size treeLevel,Size rockLevel,
    Size maxZ)=This(
  side=side,r=r,
  waterLevel=waterLevel,treeLevel=treeLevel,rockLevel=rockLevel,
  maxZ=maxZ,
  map=HeightMap(side=side, r=r,maxSeeds=\"-1")
  riverMap=RiverMap(side=side, r=r,maxSeeds=\"-1")
  grassMap=BoolMap(side=side, r=r,maxSeeds=\"-1")
  treesMap=BoolMap(side=side, r=r,maxSeeds=\"-1")
  rocksMap=BoolMap(side=side, r=r,maxSeeds=\"-1")
  )
mut method Void montainsErosion(Size rounds)=
  for r in Range(rounds) this.montainsErosionRound()
mut method Void montainsErosionRound()=(
  s=this.side()
  m=this.#map()
  o=m.#order()
  ShufflePoint.shuffle(o,r=this.#r())
  for x in Range(s) (
    for y in Range(s) (
      for (x1,y1) in o (
        xd=x+x1
        yd=y+y1//TODO: consider having defaults for MapList instead
        if m.inRange(x=xd,y=yd) (
          h0=m(x=x,y=y)
          h1=m(x=xd,y=yd)
          if h0>h1+6Size m(x=x,y=y,val=h0-4Size)
          )  
        )
      )
    )
  )
mut method Void addMontains()=(
  m=this.#map()
  m.initTops(maxZ=75\)
  var lastSize=this.side()*this.side()
  for h in Range(1\ to=this.maxZ()) (
    Debug(S"**start1 for h=%h; seeds size=%m.seeds().size() maxZ=%this.maxZ()")
    m.clearSeeds()
    Debug(S"**start2 for h=%h; seeds size=%m.seeds().size()")
    for p in m.montainsTop() ( if p.z()>=h m.addSeed(p val=h) )
    Debug(S"**start3")
    Debug(S"-- start growing for h=%h; seeds size=%m.seeds().size()")
    for attempts in Range(150\) (//grows 20 times/200 //0.1
      //Debug(S"-- attempt %attempts, seeds=%c.seeds().size()")
      if m.seeds().size()>=(lastSize*9Size)/10Size Break()
      m.grow(percent=30\)
      )
    Debug(S"-- end growing for h=%h; seeds size=%m.seeds().size()")
    lastSize:=m.seeds().size()
    )
  )
read method S msg()=(
  Debug(S"msgIn")
  g=S"Ground"
  var res=S.StringBuilder()
  for x in Range(this.side()) (
    for y in Range(this.side()) (
      for z in Range(this.map()(x=x,y=y)) (res.append(g++S";"))
      res.append(g++S";"++g++S.nl())
      )
    )
  Debug(S"msgOut")
  res.toS()
  )

