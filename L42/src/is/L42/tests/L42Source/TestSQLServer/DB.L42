JavaCode={...}
@Public Fail=Message:{[Message.Guard]}
J={
  Fail={[HasToS]}
  class method mut This #$of()
  mut method Void loadCode(S fullName, S code)[Fail]
  mut method S askEvent(S key, S id, S msg)[Fail]
  }
mut J j
class method mut This(mut J j)
@Public class method S connection()
@Public class method mut This #$of()[Fail]
  =This(J.#$of())
@Public class method mut This (mut J that)[Fail]=(
  that.loadCode(fullName=S"db.DB",code=JavaCode(url=This.connection()))
  catch J.Fail x exception Fail"%x" // in this case it is better then using .with(cause=x)?
  This(j=that)
  )
@Public mut method Void execute(S sql)[Fail]=(
  _=this.#j().askEvent(key='DB, id='execute(sql),msg=sql)
  catch J.Fail x exception Fail"%x"
  void)
@Public mut method Void executeQuery(S sql)[Fail]=(
  _=this.#j().askEvent(key='DB, id='executeQuery(sql),msg=sql)
  catch J.Fail x exception Fail"%x"
  void)
@Public mut method Bool next()[Fail]=(
  res=this.#j().askEvent(key='DB, id='next(),msg=S"")
  catch J.Fail x exception Fail"%x"
  res==S"true")
@Public mut method S val(S label)[Fail]={
  return this.#j().askEvent(key='DB, id='val(label),msg=label)
  catch J.Fail x exception Fail"%x"
  }
@Public mut method Size findColumn(S label)[Fail]={
  return Size.from(string=this.#j().askEvent(key='DB, id='findColumn(label),msg=label))
  catch J.Fail x exception Fail"%x"
  }
@Public mut method S val(Size that)[Fail]={
  return this.#j().askEvent(key='DB, id='val(that),msg=that.toS())
  catch J.Fail x exception Fail"%x"
  }
@Public mut method Void queryTables()[Fail]=(
  _=this.#j().askEvent(key='DB, id='queryTables(),msg=S"")
  catch J.Fail x exception Fail"%x"
  void)
@Public mut method Void queryColums(S tableName)[Fail]=(
  _=this.#j().askEvent(key='DB, id='queryColumns(tableName),msg=tableName)
  catch J.Fail x exception Fail"%x"
  void)
@Public mut method Void kill()=(
  _=this.#j().askEvent(key='DB, id='kill(),msg=S"")
  catch J.Fail _ (void)
  error X"unreachable")