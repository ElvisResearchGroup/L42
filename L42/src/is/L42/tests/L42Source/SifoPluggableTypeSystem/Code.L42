class method Void main()=(
  Debug(DeclassifyS(this.err(S"hello Sifo")))
  this.testDb()
  )
  
class method @Left S err(@Left S that)=that

Row=Class:Trait(Collection.map(key=S, val=S))[clear='OVal]['OVal=>S.Opt]:{
  read method S.Opt getEntry(S key)=this.immVal(key=key)
  mut method Void setEntry(S key, S value)=this.put(key=key,val=value)
  }
Rows=Collection.list(Row)

Database=Data:{
  var S name
  mut Rows rows
  mut method mut Row addRow()=(
    mut Row row = Row()
    this.#rows().#add(right=row)
    row
    )
  mut method Void removeRow(Size index)=this.#rows().remove(index)
  mut method Void setEntry(Size that, S key, S value)=(
    if that < 0Size error X"invalid row number %that"
    while that >= this.rows().size() (_=this.addRow() void)
    this.#rows().#val(that).setEntry(key=key, value=value)
    )
  read method S.Opt getEntry(Size that, S key)={
    if that < 0Size error X"invalid row number %that"
    if that >= this.rows().size() return S.Opt()<:imm S.Opt
    return this.rows().readVal(that).getEntry(key=key)
    }
  method S.List getAllEntries(S key)=S.List()(
    for i in this.rows().size().range() (
      nextValue = this.rows().readVal(i).getEntry(key=key)
      if nextValue \add(nextValue.val())
      )
    )
  }

Gui={
  mut @Left Database dbLeft
  mut @Right Database dbRight
  //class method mut This newK(mut Database dbLeft, mut @Right Database dbRight)
  class method mut This (mut @Left Database dbLeft, mut @Right Database dbRight)
  class method mut This ()=(
    capsule @Left Database dbl=Database(name=S"left",rows=Rows())
    capsule @Right Database dbr=Database(name=S"right",rows=Rows())
    This(dbLeft=dbl, dbRight=dbr)
    )
//  class method mut This ()=This(//this would be the same
//    dbLeft=\(name=S"left",rows=\())
//    dbRight=\(name=S"right",rows=\())
//    )
  }

class method Void testDb()=(
  mut Gui gui = Gui()
  gui.#dbLeft().setEntry(0Size,key=S"person",value=S"hans")
  gui.#dbRight().setEntry(0Size,key=S"person",value=S"peter")
  @Left S.Opt oName=gui.#dbLeft().getEntry(0Size,key=S"person")
  @Left S name=oName.val()
  //void
  //gui.#dbRight().setEntry(1Size,key=S"person",value=name)//Should fail SIFO
  S dName=DeclassifyS(name)
  gui.#dbRight().setEntry(1Size,key=S"person",value=dName)//thus we have to declassify
  gui.#dbLeft().setEntry(1Size,key=S"person",value=name)
  Debug(DeclassifyS(name))
  )