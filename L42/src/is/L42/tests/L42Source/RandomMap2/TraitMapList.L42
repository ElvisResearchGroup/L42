[HasToS]
E=Class.Relax:TraitE
L=Class.Relax:Trait(Collection.list(E))[clear='This]
Size side
read method Size coords(Size x,Size y)=(x*this.side())+y
mut L cells
class method mut L #default#cells(Size side)=
  L()( for i in Range(side*side) \add(E.base()) )
read method Bool inRange(Size x,Size y)=this.inRange(x) && this.inRange(y)
read method Bool inRange(Size that)=that>=0Size && that<this.side()
read method E (Size x,Size y)=this.cells().val(this.coords(x=x,y=y))
mut method Void (Size x,Size y,E val)=this.#cells().set(this.coords(x=x,y=y),val=val)
read method E (Point that)=this(x=that.x(),y=that.y())
mut method Void (Point that,E val)=this(x=that.x(),y=that.y(), val=val)
read method Size x(Size coord)=coord.mod(this.side()) 
read method Size y(Size coord)=coord/this.side()
read method S toS()=(
  var res=S""
  for y in Range(this.side()) (
    res++=S.nl()
    for x in Range(this.side()) (
      e=this(x=x,y=y)
      if e==E.base() (res++=S"@")
      else (res++=e.toS().subString(start=0\ end=1\))
      )
    )
    res
  )