_Trait = Trait : {
  E = {
    read method Bool ==(read E that)
    read method Bool !=(read E that)
    }
  Map = {
    class method E base()
    mut method Void (V.Cell2D that, E val)
    read method E (V.Cell2D that)
    }
  mut Math.Random r
  mut Map cells
  mut V.Cell2D.List seeds = V.Cell2D.List()
  Size maxSeeds
  mut Size.List order = Size.List[0\; 1\; 2\; 3\; 4\; 5\; 6\; 7\]
  mut method Void addSeed(V.Cell2D that,E val) = (
    this.#cells()(that, val=val)
    this.#seeds().add(that)
    )
    
  mut method Void initGrow()=(
    ShuffleSize.shuffle(this.#order(),r=this.#r())
    limit=this.seeds().size()-this.maxSeeds()
    ShuffleCell.shuffle(this.#seeds(),r=this.#r())
    if this.maxSeeds()>0Size (
      while this.seeds().size()>this.maxSeeds() this.#seeds().removeRight()
      )
    )
  mut method Void grow(Size percent)=(
    this.initGrow()
    for i in Range((this.seeds().size()*percent)/100Size) (
      ci = this.seeds().val(i)
      if !ci.isBorder() this.growInOrder(c=ci)
      )
    )
  mut method Void growInOrder(V.Cell2D c)=(
    for o in this.order() (
      ci=V.Delta2D.adjacent8().val(o) + c
      newSeed=this.combine(ci, seed=this.cells()(c))
      if newSeed != Map.base() (
        this.#cells()(c, val=newSeed)
        this.#cells()(ci, val=newSeed)
        this.#seeds().add(ci)
        )
      )
    )
  read method E combine(V.Cell2D that, E seed)//To override //TODO: if I write just Cell2D get bad error position.
  read method E baseCombine(V.Cell2D that, E seed)={
    oldCell = this.cells()(that)
    if oldCell != Map.base() return Map.base()
    return seed
    }
  }
  
class method Trait (class Any that)[_] =
  _Trait['Map=>that;'E=>that*'base()]