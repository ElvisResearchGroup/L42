Traits=Collection.list(Trait)

FormatPar={...}
Collecters={...}
Executers={...}
SquareBuilder={
  mut Introspection.Nested.List that
  class method mut This (mut Introspection.Nested.List that)
  class method mut This #query#squareBuilder()=This(\())
  class method Bool #shortCircutSquare()=Bool.true()
  mut method Void #squareAdd(class Any that)
    =this.#that().add(right=Introspection.Nested.from(classAny=that))
  method Library #from(S.StringBuilder stringLiteral)[MetaFail]={
    return Class:This1(types=this.that(),sql=S.#from(stringLiteral=stringLiteral))()
    catch Message.Guard g exception MetaFail"".with(cause=g)
    }
  }
//----------
Introspection.Nested.List types
S sql
Introspection.Nested r
Introspection.Method k
S.List xs
class method This(Introspection.Nested.List types,S sql,Introspection.Nested r,Introspection.Method k,S.List xs)
class method This(Introspection.Nested.List types,S sql)[MetaFail]=(
  r=types.left().meth(selector='left()).returnType().nested()
  ms=Introspection.Method.List()(for m in r.methods() (
    if m.selector().toS().startsWith(S"#immK(") \add(m)))
  if ms.size()!=1Size exception MetaFail"Not exactly one canditate #immK constructor:%ms"
  This(types=types,sql=sql,r=r, k=ms.val(0\),xs=FormatPar.xs(sql))
  )
      
method Trait field(Trait that, Size i)[_]=(
  n=Name"p%(i)()"
  f_s=Trait.LiftS(this.k().nameFromRoot().xs().val(i))['#apply()=>n]
  tSrc=Name"T%(i)"
  tDest=this.k().parameters().val(i+1Size).nested().classAny()
  (that+f_s)[tSrc=>tDest;hide=n]
  )
method Trait makeCR()[_,MetaFail]=(
  n=this.k().parameters().size()-1Size
  if n==0Size exception MetaFail"The provided type can not have zero fields: %this.r().outerName()"
  if n-1Size>=Collecters.traits().size() exception MetaFail"too many fields in the class: %this.r().outerName() max=%Collecters.traits().size()"
  var res=Collecters.traits().val(n-1Size)
  for i in n.range() ( res:=this.field(res,i=i) )//from 0 to n-1
  res[Collecters.names().val(n-1Size)=>Name"R.%this.k().selector()"]
  )
//--
method Trait par(Trait that,Size i)[_]=(
  n=Name"p%(i)()"
  tSrc=Name"T%(i)"
  tDest=this.types().val(i+1Size).classAny()
  p=this.xs().val(i)
  p_s=Trait.LiftS(p)['#apply()=>n]
  (that+p_s)[tSrc=>tDest;hide=n]
  )
method Trait makeExecuteQ()[_,MetaFail]=(
  n=this.xs().size() //ok for zero too
  if n>=Executers.traits().size() exception MetaFail"too many query parameters:%n"
  var res=Executers.traits().val(n)
  for i in n.range() ( res:=this.par(res,i=i) ) //0 to n-1
  nameDest=Name"#apply()".with(xs=this.xs())
  sql=Trait.LiftS(this.sql())['#apply()=>'sql()]
  res[Executers.names().val(n)=>nameDest]+sql
  )
//---
TraitK=Trait:{
  class method mut This (mut DB db)
  class method mut This (mut DB that)=This(db=that)
  }
method Trait ()[_]=(
  makeCR=this.makeCR()
  executeQ=this.makeExecuteQ()
  (TraitK+makeCR+executeQ)[
    'R=>this.r().classAny();
    'List=>this.types().left().classAny()
    ]
  )