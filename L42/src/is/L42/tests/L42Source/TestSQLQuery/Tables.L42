class method Library (mut DB db)[MetaFail]={
  db.queryTables()
  i=db.findColumn(label=S"TABLE_NAME")
  var res=Trait()
  while db.next() (res+=this.makeTable(db=db,name=db.val(i).val()))
  return Class:res
  catch Message.Guard g exception MetaFail"".with(cause=g)
  }
ColTrait=Trait:{T={} method T f()}
class method Trait makeTable(mut DB db,S name)[_]=(
  db.queryColums(tableName=name)
  n=db.findColumn(label=S"COLUMN_NAME")
  t=db.findColumn(label=S"TYPE_NAME")
  var res=Trait()
  while db.next() (
    res+=this.col(name=db.val(n).val(), type=db.val(t).val())
    )
  (Data:res)['This=>Name"%name.toCamelUp()"]
  )
class method class Any type(S that)[MetaFail]={
  if that==S"INTEGER" || that==S"INT" return Num
  if that==S"VARCHAR" || that==S"CHAR" return S
  if that==S"BOOLEAN" || that==S"BOOL" return Bool
  exception MetaFail"unsupported db table type %that"
  }
class method Trait col(S name,S type)[_]=(
  t=this.type(type)
  n=Name"%(name.toCamelLow())()"
  ColTrait['T=>t;ignoreSelfRename='f()=>n]
  )