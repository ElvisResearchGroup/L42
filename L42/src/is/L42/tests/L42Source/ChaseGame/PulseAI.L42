[AI]//will be used to move the player according to the keys
Bool left
Bool right
Bool up
Bool down
S text
method This withInput(S msg)={ //typed
  if msg==S.nl() return this.with(text=S"")
  return this.with(text=\text++msg)
  }
method This withInput(S msg,Bool pressed)={
  if msg==S"Left" return this.with(left=pressed)
  if msg==S"Right" return this.with(right=pressed)
  if msg==S"Up" return this.with(up=pressed)
  if msg==S"Down" return this.with(down=pressed)
  return this//return this.with(text=\text()++msg)//to fix
  }
method Double rad(Bool left,Bool right,Bool up,Bool down)={
  a45=45Double*(Vector.tao()/360Double)
  downR=a45*0Double
  downRightR=a45*1Double
  rightR=a45*2Double
  upRightR=a45*3Double
  upR=a45*4Double
  upLeftR=a45*5Double
  leftR=a45*6Double
  downLeftR=a45*7Double
  if up (
    if left return upLeftR
    if right return upRightR
    return upR
    )
  if down (
    if left return downLeftR
    if right return downRightR
    return downR
    )
  if left return leftR
  if right return rightR
  error X"unreachable"
  }
method (that,r)={
  left=this.left() && !this.right()
  right=this.right() && !this.left()
  up=this.up() && !this.down()
  down=this.down() && !this.up()
  if !up && !down && !left && !right return Pulse(rad=0\,power=0\)
  rad=this.rad(left=left,right=right,up=up,down=down)
  return Pulse(rad=rad,power=1\)
  }