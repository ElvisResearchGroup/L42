{
//members of PreferentialComposition
$:{interface
  method
  Library solveConflict(Library that,SrcDest location)
  }
SrcDest:{(Path path, Selector src,Selector dest)}
SrcDests:Collections.vector(of:SrcDest)
Libs: Collections.vector(of:Library)

class method //@private
SrcDests renameMap(Library that,S prefix,Introspection.NestedClasses intros)
  SrcDests[with intro in intros.vals() (
    round=this.renameMap(that,prefix:prefix,
      path:intro.pathFromRoot()
      ms:intro.methods())
    with r in round.vals() (use[r])
    )]
class method
SrcDests renameMap(
Library that,S prefix,Path path, Introspection.Methods ms) {
  thatI=Introspection(lib:that, path:path)
  catch exception MetaGuard.MemberUnavailable return SrcDests[]
  return SrcDests[with meth in ms.vals() (
    if this.needRename(that:thatI,meth:meth) (
      destSel=Selector.fromS(prefix++meth.selector().toS())
      use[SrcDest(path:path,src:meth.selector(),dest:destSel)]
      ))]
  }
class method
Bool needRename(
Introspection.NestedClass that,Introspection.Method meth) {
  if meth.isPrivate() (return Bool.false() )
  if meth.methodKind().equals(\class._AbstractMethod()) (return Bool.false() )
  meth2=that.method(meth.selector())
  catch exception  MetaGuard.MemberUnavailable
    return Bool.false()
  if meth2.isPrivate() (return Bool.false() )
  if meth2.methodKind().equals(\class._AbstractMethod()) (return Bool.false() )
  return Bool.true()
  }

class method
Library <<(Library that) exception MetaGuard.ClassClash,MetaGuard.MethodClash
  Refactor.compose(left:that,right:{
  //members of each produced class
 (Libs myLibs) implements $ //@private


  class method
  mut Libs #seqBuilder()
    Libs.empty()

  class method
  This #from(Libs seqBuilder)
    This(myLibs:seqBuilder)


  method
  Library << (Library that)
  exception MetaGuard.ClassClash,MetaGuard.MethodClash{
    i=Introspection(lib:that)
    map=SrcDests.empty()
    var Library acc={}
    with lib in this.myLibs().vals(), index in 1N.vals() (
      var Library libi=lib
      rens=This1.renameMap(lib,prefix:S"#"[index]"", intros:i.transitivelyNestedClasses())
      with ren in rens.vals() (
        map.add(right:ren)
        libi:=this.solveConflict(libi,location:ren)
        )
      acc:=Refactor.compose(left:acc,right:libi)
      )
    acc:=Refactor.compose(left:acc,right:that)
    with mapi in map.vals() (
      acc:=Refactor.MakePrivateSelector(mapi.dest() of:mapi.path())<<acc
      )
    return acc
    error on MetaGuard.MemberUnavailable
      Assert.CodeNotReachable"PreferentialComposition1"
    }
  method This ++ (This that)
    This(myLibs:this.myLibs()++that.myLibs())
  method
  Library foldLeft(Library resolver)
  exception MetaGuard.ClassClash,MetaGuard.MethodClash {
    if this.myLibs().isEmpty() (return {})
    var Library res=this.myLibs().left()
    with libi in this.myLibs().withoutLeft().vals() (
      res:=This[res;libi]<<resolver
      )
    return res
    }
  })
}