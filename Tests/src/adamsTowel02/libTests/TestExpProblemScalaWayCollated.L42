{reuse L42.is/AdamTowel02

RenNum:{class method Library<>< (Library that) exception Guard
  Refactor2.rename(path:\"T" into:\"Num")<><that
  }

RenPlus:{class method Library<>< (Library that) exception Guard
  Refactor2.rename(path:\"T" into:\"Plus")<><that
  }

RenNeg:{class method Library<>< (Library that) exception Guard
  Refactor2.rename(path:\"T" into:\"Neg")<><that
  }

$exp:Resource<><{
  Exp:{interface}
  T:{implements Exp}
  }

$baseT:Resource<>< {
  Exp:{interface
    method Size eval()
    }
  T:{implements Exp}
  }

$base:Resource<><Use[RenNum<><$baseT()]<>< {
  Num:{
    method Size value()
    class method Num of(Size value)
    method Size eval()
      this.value()
    }
  }

$basePlus:Resource<><Use[$base();RenPlus<><$baseT()]<>< {
  Exp:{}
  Plus:{
    method Exp left()
    method Exp right()
    class method Plus of(Exp left, Exp right)
    method Size eval()
      this.left().eval()+this.right().eval()
    }
  }

$baseNeg:Resource<><Use[$base();RenNeg<><$baseT()]<>< {
  Exp:{}
  Neg:{
    method Exp term()
    class method Neg of(Exp term)
    method Size eval()
     Size"-1" * this.term().eval()
    }
  }

$basePlusNeg:Resource<><Use[$basePlus()]<><
  Refactor2.Method[abstract:\"eval()" of:\"Num"]<><$baseNeg()

$showT:Resource<><Use[$baseT()]<><{
  Exp:{interface
    method S show()
    }
  T:{implements Exp}
  }

$show:Resource<><Refactor2.Method[abstract:\"eval()" of:\"Num"]
    <><Use[$base(); RenNum<><$showT()]<><{
  Num:{
    method S show()
      this.value().toS()
    }
  }

$showPlus:Resource<><Use[$basePlus();$show(); RenPlus<><$showT()]<><{
  Plus:{
    method S show()
      this.left().show()++S" + "++this.right().show()
    }
  }

$showNeg:Resource<><Use[$baseNeg();$show(); RenNeg<><$showT()]<><{
  Neg:{
    method S show()
      S"-("++this.term().show()++S")"
    }
  }

$showPlusNeg:Resource<><Use[$showPlus()]<><
  Refactor2.Method[abstract:\"eval()" of:\"Num";abstract:\"show()" of:\"Num"
  ]<><$showNeg()

$dbleT:Resource<><Use[$baseT()]<><{
  Exp:{interface
    method Exp dble()
    }
  T:{implements Exp}
  }

$dble:Resource<><Refactor2.Method[abstract:\"eval()" of:\"Num"]
  <><Use[$base(); RenNum<><$dbleT()]<><{
  Exp:{}
  Num:{
    method Exp dble()
      Num.of(value: this.value()*2Size)
    }
  }

$dblePlus:Resource<><Use[$basePlus();$dble(); RenPlus<><$dbleT()]<><{
  Exp:{}
  Plus:{
    method Exp dble()
      Plus.of(left: this.left().dble(), right: this.right().dble())
    }
  }

$dbleNeg:Resource<><Use[$baseNeg();$dble(); RenNeg<><$dbleT()]<><{
  Exp:{}
  Neg:{
    method Exp dble()
      Neg.of(term: this.term().dble())
    }
  }

$dblePlusNeg:Resource<><Use[$dblePlus()]<><
  Refactor2.Method[abstract:\"eval()" of:\"Num";abstract:\"dble()" of:\"Num";
  ]<><$dbleNeg()

$showDblePlusNeg:Resource<><Use[$showPlusNeg()]<><
  Refactor2.Method[abstract:\"eval()" of:\"Num";abstract:\"eval()" of:\"Plus";
  abstract:\"eval()" of:\"Neg";]<><$dblePlusNeg()

$equalsT:Resource<><Use[$baseT()]<><{
  Exp:{interface
    method Bool eql(Exp that)
    }
  T:{implements Exp
    method Bool exactEqual(T that)
    method eql(that){
      with that (on T  return this.exactEqual(that) )
      return Bool.false()
      }
    }
  }

$equals:Resource<><Refactor2.Method[abstract:\"eval()" of:\"Num"]
  <><Use[$base(); RenNum<>< $equalsT()]<><{
  Num:{
    method Bool exactEqual(Num that)
      this.value()==that.value()
    }
  }

$equalsPlus:Resource<><Use[$basePlus();$equals(); RenPlus<><$equalsT()]<><{
  Plus:{
    method Bool exactEqual(Plus that)
      this.left().eql(that.left()) & this.right().eql(that.right())
    }
  }

$equalsNeg:Resource<><Use[$baseNeg(); $equals(); RenNeg<><$equalsT()]<><{
  Neg:{
    method Bool exactEqual(Neg that)
      this.term().eql(that.term())
    }
  }

$equalsPlusNeg:Resource<><Use[$equalsPlus()]<><
  Refactor2.Method[abstract:\"eval()" of:\"Num";abstract:\"eql(that)" of:\"Num";
  abstract:\"exactEqual(that)" of:\"Num";]<><$equalsNeg()

$equalsShowPlusNeg:Resource<><Use[$equalsPlusNeg()]<><
  Refactor2.Method[abstract:\"eval()" of:\"Num";abstract:\"eval()" of:\"Plus";
  abstract:\"eval()" of:\"Neg";]<><$showPlusNeg()

Main: {
  Debug(S"Hello ExpProblemScalaWay")
  return ExitCode.normal()
} }