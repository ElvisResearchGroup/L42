{
Library forReadable,
Library forImmutable,
Library forClassable, 
Library forClassAny,
Library forLibrary,
Library forVoid
method Library select(Location.Type type, S name)
exception Refactor2.Fail{
  sel=Refactor2.Method[rename:\"f()" into:Selector.from(string:name++S"()")]
  r=type.refTo()
  g=Refactor2.redirect(path:\"T"  intoRefTo:r)
  if type.mdf().isCmpClass() (
    return sel<><g<><this.forClassable()
    catch exception Guard 
      return sel<><g<><this.forClassAny()
    void
    )
  if type.mdf().isCmpImm() (
    with r (on Location.Type.RefTo.Binded (
      cObj=r.referredClassObj()
      if Location.Type.RefTo.Binded.equalsClassObj(cObj and:Library) (
        return sel<><this.forLibrary()
        )
      if Location.Type.RefTo.Binded.equalsClassObj(cObj and:Void) (
        return sel<><this.forVoid()
        )
      ))
    return sel<><g<><this.forImmutable()
    )
    return sel<><g<><this.forReadable()
  }
class method
Library <><(Library right) {
  return K.Imm<><Use[Trait()]<><right
  error on  Refactor2.Fail X""
  }
Trait:Resource<><{
  Selector mutK
  Path of
  class method class Path #default##apply(Void of)Path
  class method class Selector #default##apply(Void mutK)Selector
  Fail:Message.$<><{ implements This3.Fail}

  method
  Bool shouldDecorate(Location.Lib that)
  exception Fail 

  class method
  Library operationTrait(Strings fNs, Location.Type.Parameter.List fTs)
  exception Fail

  //initial delegation
  method
  Library<><(Library right) exception Fail{
    l=Location.Lib(right).navigate(this.of())
    exception on Location.NotAvailable
      Fail"Nested class "[this.of()]" not found"
    m=l.method(selector:this.mutK())
    exception on Location.NotAvailable
      Fail"Selector "[this.mutK()]" not found"
    //TODO: add checks mutK is actually a factory
    if !this.shouldDecorate(l) (return right)
    fNs=m.selector().names()
    fTs=m.parameterTypes().withoutLeft()
    return Use[right]<><
      Refactor2.rename(path:\"This" into:this.of())<><
      This.operationTrait(fNs:fNs,fTs:fTs)
    error on Refactor2.Fail
      X"FieldOperation.<><"
    }
  }
}