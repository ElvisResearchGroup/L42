{(Size that)
   implements Concept.ToS Concept.Equals

  method is(subtype) {  with subtype (on class This return Bool.true()) return Bool.false()}
  method readableName()GetMyName(This)

  method toS() (
    one=This(1Size)
    S"B"[with i in 0Size.vals(upTo:32Size) (
      twoi=one<<(31Size-i)
      if (twoi & this) ==twoi ( use[S"1"] )
      else (use[S"0"])
    )]"")

  class method
  This fromS(Concept.ToS that) this.#from(builder:(
    s=that.toS()
    b=this.#builder()
    with i in 0Size.vals(upTo:s.size()) (
      c=s(i)
      if c==S"0" (b.#0())
      else if c==S"1" (b.#1())
      else error ParseFail
        "BinaryFlag32 invalid character:["[c
        ]"] contained in string:["[s]"]."
      )
    b
    ))

  class method
  mut BBuilder #builder()
    BBuilder.empty()

  ParseFail:Message.$<><{ implements Guard.ParseFail}
  BBuilder:{(var Library that, var Size count)
    class method
    mut This empty()
      This(0Size.#binaryRepr(),count:0Size)

    mut method Void #0() (
      this.count(this.count()+1Size)
      this.that(use Alu
        check leftShiftInt32(n1:this.that() n2:1Size.#binaryRepr())
        error void)
      )
    mut method Void #1() (
      this.#0()
      this.that(use Alu
        check orInt32(n1:this.that(), n2:1Size.#binaryRepr())
        error void
        )
      )
    }
  class method
  This #from(BBuilder builder) (
    if builder.count()>32Size (error ParseFail
      "BinaryFlag32 too long:"[builder.count()]" bits was required."
      )
    This(Size.#from(binaryRepr:builder.that()))
    )

  method//@_private
  This res(Library that) This(Size.#from(binaryRepr:that))
  method//@_private
  Library br() this.that().#binaryRepr()
  method
  This &(This right) this.res(use Alu
      check andInt32(n1:this.br() n2:right.br())
      error void)
  method
  This |(This right) this.res(use Alu
      check orInt32(n1:this.br() n2:right.br())
      error void)
  method
  This <<(Size right) this.res(use Alu
      check leftShiftInt32(n1:this.br() n2:right.#binaryRepr())
      error void)
  method
  This >>(Size right) this.res(use Alu
      check rightShiftInt32(n1:this.br() n2:right.#binaryRepr())
      error void)
  method
  This xor(This right) this.res(use Alu
      check xorInt32(n1:this.br() n2:right.br())
      error void)
  method
  This ~() this.res(use Alu
      check notInt32(n1:this.br())
      error void)

  read method
  Bool equals(read This that) this.that()==that.that()

  method ==(right){
    with that (on read This return this.equals(right))
    return Bool.false()
    }

  method Bool >(This right)  {
    use Alu
      check ifUInt32GrtDo(n1:right.br() n2:this.br())
      return Bool.true()
    return Bool.false()
    }


  method Bool >=(This right) {
    use Alu
      check ifUInt32GEqDo(n1:right.br() n2:this.br())
      return Bool.true()
    return Bool.false()
    }
  }
