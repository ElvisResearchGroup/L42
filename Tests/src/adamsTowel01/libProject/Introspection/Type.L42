{(fwd NestedClass root,Path path, Size memberN, Size typeN,Record record) implements Concept.ToS, Concept.Classable //@private

method is(subtype) {  with subtype (on class This return Bool.true()) return Bool.false()}
refine read method class This class() This
method readableName()GetMyName(This)
method toS() This.readableName()++S"["[this.allAsString()]"]"
method S allAsString() this.record()"AllAsString"  //just the library literal
method S strRepr() { //should become toS? is toS ok?
  n=this.referencedPath()
  S s={if n.hasExternalType() (return n.externalTypeToS())
    return S"@."[n.pathFromRoot()]""
    }
  return this.mdf().toS()++S" "++s
  }
method TypeKind typeKind() TypeKind.fromS(this.record()"TypeKind") //{Normal,Alias,AliasUnresolvable}
method Mdf mdf() {//error on AliasUnresolvable
  if !( this.typeKind().equals(\class._AliasUnresolvable())) (return Mdf.fromS(this.record()"Mdf"))
  error Guard.Operation"AliasUnresolvable class, mdf unknown"
  }

method //@private
NestedClass  referencedPath(Doc.Annotation ann) {
  if ann.hasReferredClass() (return Introspection(clazz:ann.referredClass()))
  S sPath=ann.toS()(start:1Size)
  if sPath==S"" (return this.root())
  Path path=Path.fromS(sPath)
  return this.root().nestedClassFromRoot(path)
  catch exception MetaGuard.NestedClassUnavailable x error X"Type refers to unexistent path"
  }
method NestedClass referencedPath()
  this.referencedPath(ann:this.record().docOf(path:Path"Path").annotations().left())

method NestedClass resolvedReferencedPath() {//error on AliasUnresolvable
  if !(this.typeKind().equals(\class._AliasUnresolvable())) (
    return this.referencedPath(ann:this.record().docOf(path:Path"ResolvedPath").annotations().left())
    )
  error Guard.Operation"AliasUnresolvable class, mdf unknown"
  }

method Bool partial() this.record()"Ph"==S"true"
method Bool resolvedPartial() this.record()"ResolvedPh"==S"true" //error on Normal? or the same of partial()
method S suffix() { //error on Normal
  if this.typeKind().equals(\class._Normal()) (
    error Guard.Operation"Normal types have no suffix"
    )
  return this.record()"Ph"
  }
method Doc doc() this.record().docOf(path:Path"Doc")

method Bool isInterface(){
  str=this.allAsString()
  if str.contains(S"Void") (return Bool.false())
  if str.contains(S"Any") (return Bool.true())
  if str.contains(S"Library") (return Bool.false())
  nc=this.referencedPath()
  return nc.classKind().equals(\class._Interface())
  }

method Bool isAny()  this.allAsString().contains(S"Any")
method Bool isVoid()  this.allAsString().contains(S"Void")
method Bool isLibrary()  this.allAsString().contains(S"Library")

method Bool isPrimitive() (
  str=this.allAsString()
  str.contains(S"Void") | str.contains(S"Any") | str.contains(S"Library")
  )

}


