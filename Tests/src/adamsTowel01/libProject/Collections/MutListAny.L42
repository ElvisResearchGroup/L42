{(var Cell head,var Size size)
  implements Concept.ToS

  Cell:{interface Any
    method Any get(Size that)
    method Cell set(Size that, Any val,Bool add)
    method Cell remove(Size that)
    }

  CellNext:{(Any val,Cell next) implements Cell
    method get(that) {
      if that==0Size (return this.val())
      return  this.next().get(that-1Size)
      }
    method set(that,val,add) {
      if that!=0Size ( return This(val:this.val(),next:this.next().set(that-1Size, val:val,add:add)) )
      if add (return This(val:val,next:this) )
      return This(val:val,next:this.next())
      }
    method remove(that) {
      if that!=0Size ( return This(val:this.val(),next:this.next().remove(that-1Size)))
      return this.next()
      }
    }

  CellEnd:{() implements Cell
    method get(that) error X"CellEnd.get"
    method set(that,val,add) {
      if !add (error X"CellEnd.set1")
      if that==0Size (return CellNext(val:val,next:this) )
      error X"CellEnd.set2"
      }
    method remove(that)
      error X"CellEnd.remove"
    }

  class method
  mut This empty()
     This(head:CellEnd(),size:0Size)

  read method
  Any val(Size that) (
    this.checkBound(that)
    this.head().get(that)
    )

  mut method
  Void (Size that,Any val) (
    this.checkBound(that)
    this.head(this.head().set(that,val:val,add:Bool.false()))
    )

  mut method
  Void add(Size that, Any val) (
    this.checkBoundAdd(that)
    this.head( this.head().set(that, val:val,add:Bool.true()) )
    this.size(this.size()+1Size)
    )
  mut method
  Void remove(Size that) (
    this.checkBound(that)
    this.head( this.head().remove(that) )
    this.size(this.size()-1Size)
    )

  method
  This without(Size that) (
    this.checkBound(that)
    This(head: this.head().remove(that), size:this.size()-1Size )
    )

  method
  This withAlso(Size that, Any val) (
    this.checkBoundAdd(that)
    This(head: this.head().set(that,val:val, add:Bool.true()), size:this.size()+1Size )
    )

  method
  This with(Size that, Any val) (
    this.checkBound(that)
    This(head: this.head().set(that,val:val, add:Bool.false()), size:this.size()+1Size )
    )

  read method
  Void checkBound(Size that) (
    if that<0Size (error CollectionErrors.negBound(that, list:this) )
    if this.size()<=that (error CollectionErrors.outBound(that, list:this) )
    )
  read method
  Void checkBoundAdd(Size that) (
    if that<0Size (error CollectionErrors.negBound(that, list:this) )
    if this.size()<that (error CollectionErrors.outBound(that, list:this) )
    )
    
  method toS() {
    var S ret = S"["
    var S sep = S""
    var Cell c = this.head()
    
    loop (
      //Debug(S"MutListAny.toS() loop pass with ret='"[ret]"', sep='"[sep]"'")
      with c (
        on CellNext (
          ret := ret ++ sep
          with v=c.val() (
            on Concept.ToS ret := ret ++ v.toS()
            on Void  ret := ret ++ S"void"
            on Library ret := ret ++ S"{..}"
            default        ret := ret ++ S"?"
            )
          c := c.next()
          )
        default
          return ret++S"]"
        )
      sep := S"; "
      )
    }

  }