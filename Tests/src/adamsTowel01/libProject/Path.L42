{//@plugin is.L42.connected.withSafeOperators
 //@pluginPart java.util.List
  implements Concept.Base
  class method
  Library #pluginUnresponsive(Library binaryRepr)
    binaryRepr  
  //class method mut This #from(Library binaryRepr)
  //read method Library #binaryRepr()

  class method
  This from(Concept.ToS string) {
    return PlgPath.rawFromS(string.toS())
    catch error Any error ParseFail"The provided string is not a valid path: "[string]""
    }
  ParseFail:Message.$<><{ implements Guard.ParseFail}

  class method
  This #from(S.SBuilder builder)
    This.from(string:S.#from(binaryRepr: builder.#binaryRepr()))
  class method
  mut S.SBuilder #builder()
    S.SBuilder.empty()

  method
  Strings names()
    Strings[with i in 0Size.vals(upTo:this.nameSize()) (use[this.name(i)])]

  class method
  This from(read Strings names) {
    if names.isEmpty() (return This"This")
    s=S""[with n in names.vals() (use[n sep:S"."])]""
    return This.from(string:s) //(end:s.size()-1Size)
    }

  class method
  This fresh()
    This.fresh(like:This"Fresh")

  method
  S name(Size that)
    PlgPath.name(this,index:that)
  method
  Size nameSize()
    this.size()
  method toS()
    PlgPath.toS(this)
  method
  This ++(This right)
    PlgPath.concat(this and:right)
  method
  Bool isUnique()
    PlgPath.isUnique(this)
  class method
  This fresh(This like)
    PlgPath.fresh(like)
  //from Java
  method
  Size size()
  //from decoration: equals

  PlgPath:{
    //@plugin is.L42.connected.withSafeOperators
    //@pluginPart is.L42.connected.withSafeOperators.refactor.PathAdapter
    class method
    Library #pluginUnresponsive(Library binaryRepr)
      binaryRepr
    class method
    S name(This1 that,Size index)
    class method
    This1 concat(This1 that,This1 and)
    class method
    Bool isUnique(This1 that)
    class method
    This1 fresh(This1 that)
    class method 
    This1 rawFromS(S that)
    class method
    S toS(read This1 that)
    }
  }