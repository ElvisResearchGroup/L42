{ implements Concept.ToS Concept.Equals, Concept.Classable//@private
  class method mut This _private(Library _binaryRepr)
  read method Library _binaryRepr()

  ParseFail:{}//will be fixed in adam2
  method is(subtype) {  with subtype (on class This return Bool.true()) return Bool.false()}
  refine read method class This class() This
  method readableName()GetMyName(This)

  class method
  This fromS(Concept.ToS that)
    This.#from(binaryRepr: that.toS().binaryRepr())

  class method This #from(S.SBuilder builder)
    This.#from(binaryRepr: builder.binaryRepr())

  class method mut S.SBuilder #builder() S.SBuilder.empty()

  class method
  mut This #from(Library binaryRepr) (
    use SafeOperators
      check ifInvalidDo(selector:binaryRepr)
      error This.parseError(S.#from(binaryRepr: binaryRepr))
    This._private(_binaryRepr:binaryRepr)
    )
  class method
  Any parseError(S that)
    S"Selector.UnsatisfactoryErrorToAvoidCycle, replace in AdamT2"

  class method
  This from(S name,Strings names) {
    if names.isEmpty() (return This.fromS(name++S"()"))
    pars=S"("[with n in names.vals() (use[n++S","])]""
    return This.fromS(name++pars(end:pars.size()-1Size)++S")")
    //This.fromS(name[pars(end:#-1Size)]")")
    }
  method S nameAsField() {
    n=this.name()
    if n(0Size)==S"#" (return n(start:1Size))
    return n
    }
  method S prefixAsField() {
    if this.toS()(0Size)==S"#" (return S"#")
    return S""
    }
  method Library binaryRepr() this._binaryRepr()


  method toS() S.#from(binaryRepr: this._binaryRepr())

  method
  Bool == (This that) {
    use Alu
      check ifStringEqualDo(s1:this.binaryRepr(),s2:that.binaryRepr())
      return Bool.true()
    return Bool.false()
    }
  method equals(that){
    with that (on This return this==that)
    return Bool.false()
    }

  method
  Bool isOperator(){
    if this.name().size()==0Size (return Bool.true() )
    return this.name()(0Size)==S"#"
    }
  method
  S name() (
    str=this.toS()
    str(end:str.indexOfLeft(val:S"("))
    )

  method
  Strings names(){
    var S str=this.toS()
    str:=str(start:str.indexOfLeft(val:S"(")+1Size, end:str.size()-1Size)++S","
    // from foo(a,b,c) to a,b,c,
    result=Strings.empty()
    if str==S"," (return result)
    while str!=S"" (
      pos=str.indexOfLeft(val:S",")
      result.add(right:str(end:pos))
      str:=str(start:pos+1Size)
      )
    return result
  }

  class method
  Selector fresh(Selector that) {
    var S name=that.name()
    if name==S"" (name:= S"fresh")
    fName=SafeOperators.freshName(name)
    var S tot=fName++S"("
    with n in that.names().vals() (
      tot++=n++S","
      )
    tot:=tot(start:0Size end:tot.size()-1Size)++S")"
    return Selector.fromS(tot)
    }


    class method //will be private
    S leftAsASCIIUpperCase(S that){
      index=S.aSCIILowerCase().indexOfLeft(val:that.left())
      if index>=0Size  ( return that.withLeft(S.aSCIIUpperCase()(index) ))
      return that
      }

    method
    This with(S prefix) {
      S newName=prefix++This.leftAsASCIIUpperCase(this.name())
      return Selector.from(name:newName, names:this.names())
      }

  //we still need to add check validity, and fields name, list parameters, and ways to add/remove parameters
  }