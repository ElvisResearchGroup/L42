{' generic linked list class, elements of class Elem
 mut _new(var mut Cell head)

class method
This ()
  This._new(head:CellEnd())
mut method
Bool isEmpty() {
  with x=this.head() (
    on CellEnd  return True()
    default return False() 
    )
  }

read method
Elem top()
  this.head().#inner()

mut method
mut Iterator vals()
  this.head().vals(terminate:True())

mut method
mut Iterator valsCut()
  this.head().vals(terminate:False())
 
mut method
mut This #pop() {
  return This._new(head:this.head().#next()) 
  catch exception (on Void error S"PopOnEmpyList")
  }    

read method
capsule This clone() This._new(head:this.head().clone())

read method
capsule This ++(read This that) {
  var mut This res=this.clone()
  with e in that.vals() (res:=res.#add(e))
  return res
  }

class method
mut This #begin()
  This()

mut method
mut This #add(Elem that) {
  this.head=CellNext(elem:that,nextCell:this.#head())
  return this
  }

mut method
mut This #end() (
  var res=This()
  with elem in this.vals() (
    res:=res.#add(elem)
    )
  res
  )

Elem:{}

Cell:{interface
    
  method
  mut Iterator vals(Bool terminate)
   
  method
  Cell #next() exception Void
    
  method
  Elem #inner()
  
  method
  Void #checkEnd()
  }
  
CellEnd:{() implements Cell
  method
  vals(terminate)
    Iterator(this,terminate:terminate)

  method
  #next()
    exception void
    
  method
  #inner()
    error S"InnerOnCellEnd, should not happen"
    
  method
  #checkEnd() 
    void 'ok, is the end
  }
  
CellNext:{(Elem elem, Cell nextCell) implements Cell
  
  method
  vals(terminate) {
    startPoint=CellNext(elem:this.elem(), nextCell:this), 'needed, at the start needs to be in "-1"
    return Iterator(startPoint,terminate:terminate)
    }
    
  method
  #next()
    this.nextCell()
    
  method
  #inner() 
    this.elem()
    
  method
  #checkEnd()
    with cell=this.nextCell() (
      on CellNext error S"
      'IterationNotComplete,
      '  use valsCut() to allows incomplete iterations
      "
      )
  }
  
Iterator:{mut (var Cell that,Bool terminate)

  mut method
  Void #next() exception Void {
    x=this.that().#next()
    with x ( on CellEnd exception void ) 'it is already over
    return this.that(x) 
    }
  
  read method
  Void #checkEnd()
    if this.terminate() ( this.that().#checkEnd() )
  
  read method
  Elem #inner()
    this.that().#inner()' we never call this on CellEnd, #next prevents it
  
  read method
  Void #close()
    void
  }
}