{ interface implements Guard

 RecordWrapper:// can not be private, I need to Patch it.
    {
    class method S libToS(Library that) S"TO FIX"

    class method Library recordWrapperToSTrait(){ implements MetaGuard
      method toS() This.myName()++This1.libToS(this.lib())
      method Library lib()
      }
    class method Library recordWrapperTrait(){ implements MetaGuard
      method kind() This.myName()
      method text() S""
      method responseOf() Message.Opt()
      method myName()GetMyName(This)
      method equals(that){
        with that (on This return this==that)
        return Bool.false()
        }
      method class() This
      method classAny() This
      class method This(Introspection.Record record, Library lib)
      method Bool != (This that)
        !(this==that)
      method Bool == (This that)
        Message.EqualMessages(this,other:that)
      class method S libName()
      class method Void (Library lift) exception This (
        record=Introspection.Record(lib:lift)
        x=record"Kind"
        if x==This.libName() (
          exception This(record:record,lib:lift)
          )
        )
      }

    class method Library <<(Library that) (
      //either that provides a toS, or I have a default impl, that requires lib()
      res=SafeOperatorsAccess.compose(left:that,right:This.recordWrapperTrait())
      Library open=  {
        return SafeOperatorsAccess.compose(left:res,right:This.recordWrapperToSTrait())
        catch error Library return res
        }
      {
        return SafeOperatorsAccess.compose(left:open,right:{
            class method mut This #mut(fwd Introspection.Record record, fwd Library lib)
            class method This(Introspection.Record record, Library lib) This.#mut(record:record,lib:lib)
            })
        catch error Library return open
        }


      )
    }
SourceUnfit:RecordWrapper<<{class method S libName()S"SourceUnfit"}
IncoherentRedirectMapping:RecordWrapper<<{class method S libName()S"IncoherentRedirectMapping"}
ClassClash:RecordWrapper<<{class method S libName()S"ClassClash"}
MethodClash:RecordWrapper<<{class method S libName()S"MethodClash"}
InvalidOnTopLevel:RecordWrapper<<{class method S libName()S"InvalidOnTopLevel"}
PrivacyCoupuled:RecordWrapper<<{class method S libName()S"PrivacyCoupuled"}
ParameterMismatch:RecordWrapper<<{class method S libName()S"ParameterMismatch"}

MemberUnavailable:{interface implements MetaGuard}
DispatchMemberUnavailable:{
  class method Void(Library that)
  exception MemberUnavailable(
    record=Introspection.Record(lib:that)
    x=record"Kind"
    if x== S"MemberUnavailable" (
      if record"InvalidKind".contains(S"Method") (
        exception MethodUnavailable(record:record,lib:that) )
      exception NestedClassUnavailable(record:record,lib:that)
      )
    )
  }
NestedClassUnavailable:RecordWrapper<<{(Path path,Bool isPrivate) implements MemberUnavailable
  method toS() This.myName()++S"[path:"[this.path()]",isPrivate:"[this.isPrivate()]"]"
  class method This (Introspection.Record record, Library lib)
    This(path:Path.fromDocInternalizedS(record"Path"),isPrivate:Bool.fromS(record"IsPrivate"))
  class method S libName()S"MemberUnavailable"
  }
MethodUnavailable:RecordWrapper<<{(Path path,Selector selector,Bool isPrivate) implements MemberUnavailable
  method toS() This.myName()++S"[path:"[this.path()]", selector:"[this.selector()]",isPrivate:"[this.isPrivate()]"]"
  class method This (Introspection.Record record, Library lib)
    This(path:Path.fromDocInternalizedS(record"Path"),selector:Selector.fromS(record"Selector"),isPrivate:Bool.fromS(record"IsPrivate"))
  class method S libName()S"MemberUnavailable"
  }
}