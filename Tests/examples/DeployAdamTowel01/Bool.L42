{(Library binaryRepr)<:Concept.ToS Concept.Equals Concept.Named'@private
  method
  myName()GetMyName(This)
  method
  Void #checkTrue() exception Void 
    use Alu
      check ifInt32EqualDo(n1:this.binaryRepr() n2:0N.binaryRepr())
      exception void
  type method
  This true()
    This(binaryRepr:1N.binaryRepr())
  type method
  This false()
    This(binaryRepr:0N.binaryRepr())

  method
  This &(This that){
    if this (return that)
    return this
    }
  method
  This |(This that){
    if this (return this)
    return that
    }
  method
  This !(){
    if this (return This.false())
    return This.true()
    }
  method
  toS(){
    if this (return S"true")
    return S"false"
    }
  type method
  This fromS(read Concept.ToS that){
    if that.toS()==S"true" (return This.true())
    if that.toS()==S"false" (return This.false())
    error This.parseError(that.toS())
    }
  method
  This ==(This that) {
    if this (return that)
    return !that
    }
  method
  This !=(This that) !(this==that)
  method
  equals(that){
    with that (on This return this==that)
    return Bool.false()
    }
  type method
  Any parseError(S that)
    S"Bool.UnsatisfactoryErrorToAvoidCycle, replace in AdamT2"
}