[HasToS]
NativeNested i
class method This(NativeNested native)=\(i=native)

@Public class method This (class Any that)=\(native=\.fromClass(that))
  
@Public class method This (Library library)=\(native=\.fromLibrary(library))
  
@Public method This nested(Name name)=\(native=this.i().nestedByName(name=name.that()))

@Public method S outerName()=this.i().outerName()

@Public method This outer()=\(native=this.i().outer())

@Public method Doc outerDoc()=\(this.i().outerDoc())

@Public method Doc innerDoc()=\(this.i().innerDoc())

@Public method Nested$List nesteds()=\()((
  size=this.i().nestedNum()
  var i=0I
  while i<size (
    \add(Nested$(native=this.i().nestedIn(i)))
    i+=1I//sadly, range will be added later
    )))
@Public method Method$List methods()=\()((
  size=this.i().methodNum()
  var i=0I
  while i<size (
    \add(Method$(this.i().methodIn(i)))
    i+=1I
    )))

@{
If the name has empty path, the method named as name.selector() is returned,
otherwise the name path and selector are fetched from the root of this.
} @Public method Method$ meth(Name name) = {
  (path,selector)=name
  if path==S"This" return \meth(selector=selector)
  return \nested(name=name).meth(selector=selector)
  }
method Method$ meth(S selector)={
  size=this.i().methodNum()
  var i=0I
  while i<size (
    mi=Method$(this.i().methodIn(i))
    if mi.nameFromRoot()<:Name.selector()==selector return mi
    i+=1I
    )
  ns=S$List()(for m in this.methods() \add(m.selector()) )
  error Collection$NoElementInOpt"Method %selector not in methods list: %ns"
  }
@Public method Type$List implements()=\()((
  size=this.i().implementedNum()
  var i=0I
  while i<size (
    \add(Type$(this.i().implementedIn(i)))
    i+=1I//sadly, range will be added later
  )))
@Public method Bool hasOuter()=this.i().hasOuter()

@Public method Bool hasHiddenImplements()=this.i().hasHiddenImplements()

@Public method Bool isClose()=this.i().isClose()

@Public method Bool isInterface()=this.i().isInterface()

@Public method Bool isBinded()=this.i().isBinded()

@Public method S isCoherent()=this.i().isCoherent()

@Public method S toFullS()=this.i().toFullString()

@Public method This root()=\(native=this.i().root())  

@Public method Name nameFromRoot()=\.of(this.i().nameFromRoot())

@Public method S position()=this.i().position()

@Public method class Any classAny()=this.i().classAny()

method toS()=this.i().toS()

@Public method S msgTitle(S text)=
  S"Message %(Name$Mapping().pathName(this.classAny()))%this.superInterfaces():%S.nl()%text"

method S className()=Name$Mapping().pathName(this.classAny())

method S superInterfaces()={
  i=this.implements()
  if i.isEmpty() return S""
  var res=S"("++i.left().info()<:This.className()
  for t in i.withoutLeft() (res++=S", "++t.info()<:This.className())
  return res++S")"
  } 