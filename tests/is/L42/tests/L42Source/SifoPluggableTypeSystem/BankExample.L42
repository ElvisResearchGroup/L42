Account={
  class method I interestRate()=2I
  class method I overdraftLimit()=I"-5000"
  class method I dailyLimit()=I"-1000"
  var @Top I interest
  mut @Top State state
  var Bool lock
  class method S statement(read State state)=S"""%
    |Current balance=%state.balance()
    |Current withdraw=%state.withdraw()
    """
  class method mut This (@Top I interest, mut @Top State state, Bool lock)
  class method mut This()=This(balance=0\)
  class method mut This(I balance) = This(
    interest=0I,
    state=State(balance=balance, withdraw=0I)
    lock=Bool.false()
    )
  
  read method @Top I calculateInterest()=(
    @Top I balance = this.state().balance()
    @Top I totInterests = balance * This.interestRate()
    totInterests / 36500I
    )
  read method @Top I estimatedInterest(I daysLeft)=(
    @Top I moreInterest = daysLeft * this.calculateInterest()
    this.interest() + moreInterest
    )
  read method @Top Bool creditWorthy(I amount)=(
    @Top I balance = this.state().balance()
    balance >= amount
    )
  mut method Void doLock()=this.lock(Bool.true())
  mut method Void unLock()=this.lock(Bool.false())
  }
State=Data:{
  var I balance,var I withdraw
  mut method Bool updateBalance(I that)={
    I newBalance = this.balance() + that
    if newBalance < Account.overdraftLimit() return Bool.false()
    this.balance(newBalance)
    return Bool.true()
    }
  mut method Bool undoUpdateBalance(I that)={
    I newBalance = this.balance() - that
    if newBalance < Account.overdraftLimit() return Bool.false()
    this.balance(newBalance)
    return Bool.true()
    }
  mut method Bool update(I that)={
    var newWithdraw = this.withdraw()
    if that < 0I (
      newWithdraw += that
      if newWithdraw < Account.dailyLimit() return Bool.false()
      )
    overdraw = newWithdraw >= Account.dailyLimit() && !this.updateBalance(that) 
    if overdraw return Bool.false()
    this.withdraw(newWithdraw)
    return Bool.true()
    }
  mut method Bool undoUpdate(I that)={
    var newWithdraw = this.withdraw()
    if that < 0I (
      newWithdraw -= that
      if newWithdraw < Account.dailyLimit() return Bool.false()
      )
    overdraw = newWithdraw >= Account.dailyLimit() && !this.undoUpdateBalance(that) 
    if overdraw return Bool.false()
    this.withdraw(newWithdraw)
    return Bool.true()
    }
  }

Application={
  mut Account account
  class method mut This (mut Account account)
  class method mut This ()=(
    mut Account acc=Account()
    This(account=acc)
    )
  mut method Void nextDay()=(
    mut @Top State state = this.#account().#state()
    state.withdraw(0I)
    this.#account().interest(\interest + \calculateInterest)
    )
   mut method Void nextYear()=(
    mut @Top State state = this.#account().#state()
    @Top I interest = this.account().interest()
    @Top I newBalance = state.balance() + interest
    state.balance(newBalance)
    this.#account().interest(0I)
    )
  }
Transaction={
  class method @Top Bool transfer(mut Account source, mut Account destination, I amount)={
    if !This.lockForTransfer(source=source, destination=destination) return Bool.false()
    mut @Top State sourceState = source.#state()
    mut @Top State destinationState = destination.#state()
    if (amount <= I"0") return Bool.false() 
    @Top Bool sourceKo = !sourceState.update(amount * I"-1")
    if sourceKo return Bool.false()
    @Top Bool destinationKo = !destinationState.update(amount)
    if destinationKo (
      _=sourceState.undoUpdate(amount * I"-1")
      return Bool.false()
      )
    source.unLock()
    destination.unLock()
    return Bool.true()
    }
  class method Bool lockForTransfer(mut Account source, mut Account destination)={
    if source.lock() return Bool.false()
    if destination.lock() return Bool.false()
    source.doLock()
    destination.doLock()
    return Bool.true()
    }
  }
Main={class method Void()=(
  mut Application a=Application()
  mut Account bob=Account(balance=50\)
  mut Account alice=Account(balance=65\)
  _=Transaction.transfer(source=alice,destination=bob,amount=5\)
  @Top S txt=Account.statement(state=bob.state())
  Debug(DeclassifyS(txt))
  void
  )}